buildscript {
  ext {
    indraVersion = "2.1.1"
  }
}

plugins {
  id "net.kyori.indra" version "$indraVersion" apply false
  id "net.kyori.indra.checkstyle" version "$indraVersion" apply false
  id "net.kyori.indra.license-header" version "$indraVersion" apply false
  id "net.kyori.indra.publishing.sonatype" version "$indraVersion"
}

group("net.kyori")
version("5.0.0-SNAPSHOT")
description("An event bus system.")

subprojects {
  if(name != "event-bom") {
    apply plugin: "net.kyori.indra"
    apply plugin: "net.kyori.indra.checkstyle"
    apply plugin: "net.kyori.indra.license-header"

    repositories {
      mavenLocal()
      mavenCentral()
      sonatypeSnapshots()
    }

    dependencies {
      checkstyle("ca.stellardrift:stylecheck:0.1")
      testImplementation("com.google.guava:guava-testlib:31.0.1-jre")
      testImplementation("com.google.truth:truth:1.1.3")
      testImplementation("com.google.truth.extensions:truth-java8-extension:1.1.3")
      testImplementation(platform("org.junit:junit-bom:5.8.2"))
      testImplementation("org.junit.jupiter:junit-jupiter-api")
      testImplementation("org.junit.jupiter:junit-jupiter-engine")
    }
  }

  apply plugin: "net.kyori.indra.publishing"

  indra {
    github("KyoriPowered", "event") {
      ci = true
    }
    mitLicense()

    configurePublications {
      pom {
        developers {
          developer {
            id = "kashike"
            timezone = "America/Vancouver"
          }
        }
      }
    }
  }
}
