plugins {
    id 'java'
    id 'maven'
    id 'signing'
    id 'net.minecrell.licenser' version '0.3'
}

group 'net.kyori'
version '1.0.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:all', '-Xlint:-path', '-parameters']
    options.deprecation = true
    options.encoding = 'UTF-8'
}

license {
    header project.file('header.txt')
    include '**/*.java'
    newLine false
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'com.github.ben-manes.caffeine:caffeine:2.5.2'
    compile 'com.google.code.findbugs:jsr305:3.0.2'
    compile 'com.google.errorprone:error_prone_annotations:2.0.15'
    compile 'com.google.guava:guava:21.0'
    compile 'org.ow2.asm:asm-debug-all:5.1'
    compile 'org.slf4j:slf4j-api:1.7.25'
    testCompile 'junit:junit:4.12'
}

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

signing {
    required { !project.version.endsWith('-SNAPSHOT') && gradle.taskGraph.hasTask('uploadArchives') }
    sign configurations.archives
}

if(project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    name project.name

                    issueManagement {
                        system 'GitHub Issues'
                        url 'https://github.com/KyoriPowered/event/issues'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'https://opensource.org/licenses/MIT'
                        }
                    }

                    scm {
                        connection 'scm:git@github.com:KyoriPowered/event.git'
                        developerConnection 'scm:git@github.com:KyoriPowered/event.git'
                        url 'https://github.com/KyoriPowered/event/'
                    }
                }
            }
        }
    }
}
